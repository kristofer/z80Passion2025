   10 ON ERROR GOTO 750
   20 REM 3D CUBE EXAMPLE
   30 REM By Dean Belfield
   40 :
   50 MODE 1
   60 PRINT "3D Cube Demo - VDP 1.04 Double-buffering!"
   70 PRINT
   80 INPUT "SOLID (S) OR WIREFRAME (W)";F$: FILLED%=F$="S"
   90 MODE 136
  100 VDU 29,640;512;:OD=128
  110 :
  120 REM Initialise the data
  130 READ CN%
  140 DIM X%(CN%),Y%(CN%),Z%(CN%),A%(CN%),B%(CN%)
  150 FOR I%=1 TO CN%: READ X%(I%),Y%(I%),Z%(I%):NEXT
  160 READ TS%
  170 DIM S%(TS%,5)
  180 FOR I%=1 TO TS%: READ S%(I%,1),S%(I%,2),S%(I%,3),S%(I%,4),S%(I%,5):NEXT
  190 REM Variables
  200 THETA=0:PSI=0:PHI=0
  210 XD=0:YD=0
  220 SD=1024
  230 REM Transform
  240 FOR I%=1 TO CN%
  250   REM Rotate
  260   XX=X%(I%):YY=Y%(I%):ZZ=Z%(I%)
  270   Y=YY*COS(PHI/180*PI)-ZZ*SIN(PHI/180*PI)
  280   ZZ=YY*SIN(PHI/180*PI)+ZZ*COS(PHI/180*PI)
  290   X=XX*COS(THETA/180*PI)-ZZ*SIN(THETA/180*PI)
  300   ZZ=XX*SIN(THETA/180*PI)+ZZ*COS(THETA/180*PI)
  310   XX=X*COS(PSI/180*PI)-Y*SIN(PSI/180*PI)
  320   YY=X*SIN(PSI/180*PI)+Y*COS(PSI/180*PI)
  330   REM Transform
  340   XX=XX+XD:YY=YY+YD
  350   REM Perspective
  360   A%(I%)=XX*SD/(OD-ZZ)
  370   B%(I%)=YY*SD/(OD-ZZ)
  380 NEXT
  390 REM Draw
  400 CLS
  410 I%=RND(-55)
  420 FOR I%=1 TO TS%
  430   GCOL 0,S%(I%,5)
  440   C1%=S%(I%,1):C2%=S%(I%,2):C3%=S%(I%,3):C4%=S%(I%,4)
  450   X1=A%(C1%):X2=A%(C2%):X3=A%(C3%):X4=A%(C4%)
  460   Y1=B%(C1%):Y2=B%(C2%):Y3=B%(C3%):Y4=B%(C4%)
  470   IF X1*(Y2-Y3)+X2*(Y3-Y1)+X3*(Y1-Y2) > 0 THEN GOTO 530
  480   IF FILLED%=0 THEN GOTO 520
  490   MOVE X1,Y1:MOVE X2,Y2:PLOT 85,X3,Y3
  500   MOVE X3,Y3:MOVE X4,Y4:PLOT 85,X1,Y1
  510   GOTO 530
  520   MOVE X1,Y1:PLOT 5,X2,Y2:PLOT 5,X3,Y3:PLOT 5,X4,Y4:PLOT 5,X1,Y1
  530 NEXT
  540 PHI=PHI+4
  550 THETA=THETA-1
  560 VDU 23,0,195
  570 GOTO 240
  580 REM DATA
  590 DATA 8
  600 DATA -20,20,20
  610 DATA 20,20,20
  620 DATA -20,-20,20
  630 DATA 20,-20,20
  640 DATA -20,20,-20
  650 DATA 20,20,-20
  660 DATA -20,-20,-20
  670 DATA 20,-20,-20
  680 DATA 6
  690 DATA 1,2,4,3,9
  700 DATA 7,8,6,5,10
  710 DATA 2,6,8,4,11
  720 DATA 3,7,5,1,12
  730 DATA 3,4,8,7,13
  740 DATA 1,5,6,2,14
  750 MODE 0
